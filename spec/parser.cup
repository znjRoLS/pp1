package rosko.bojan;

import java_cup.runtime.*;
import org.apache.logging.log4j.LogManager;
import org.apache.logging.log4j.Logger;
import rs.etf.pp1.symboltable.Tab;
import rs.etf.pp1.symboltable.concepts.*;

import rosko.bojan.SymbolCounter;
import rosko.bojan.SemanticContext.SemanticSymbol.*;
import rosko.bojan.SemanticContext.SemanticSymbol;

import rosko.bojan.Pair;

//////////////////////////////////////////////

parser code {:
    Logger logger = LogManager.getLogger(Parser.class);
    Scanner scanner;
    Symbol currSym;

    public SemanticContext context = new SemanticContext();

    void report_error(String msg) {
        logger.error(msg);
        System.err.println("ERROR: " + msg);
    }
    void report_info(String msg) {
        logger.info(msg);
        //System.err.println("INFO: " + msg);
    }
    void dump() {
        context.dumpTable();
    }
:}

init with {:
    scanner = this.getScanner();
    Tab.init();

    Tab.insert(Obj.Con, "void", Tab.nullType);

    action_obj.ctx = context;
:}

scan with {:
    currSym = scanner.next_token();
    if (currSym != null && currSym.value != null) {
        report_info("Parsed symbol: " + currSym.value + " - " + currSym);
    }
    return currSym;
:}

action code {:

    enum ValueType {
        LVALUE,
        RVALUE
    }

    final Pair<Integer, ValueType> EMPTY_VALUE = new Pair<Integer, ValueType>(-1, ValueType.RVALUE);

    Obj currentMethod = null;
    Struct currentVarDeclType;

    SemanticContext ctx;


    void report_error(String msg) {
        this.parser.logger.error(msg);
        //System.err.println("ERROR: " + msg);
    }
    void report_info(String msg) {
        this.parser.logger.info(msg);
        //System.err.println("INFO: " + msg);
    }

:}

////////////////////////////////////////////////

// TERMINALS
////////////////////////////////////////////////

// RESERVED WORDS
terminal PROGRAM, ERROR;
terminal NEW, RETURN, VOID;
terminal FOR, BREAK, CONTINUE;
terminal CLASS, CONST, EXTENDS, STATIC;
terminal IF, ELSE;
terminal PRINT, READ;

// IDENTIFICATOR AND LITERALS
terminal ID, CONST_NUMBER, CONST_CHAR, CONST_BOOL;

// OPERATORS
terminal ADDITION, SUBTRACTION, MULTIPLICATION, DIVISION, MODULO;
terminal EQUAL, NOT_EQUAL, GREATER, GREATER_EQUAL, LESS, LESS_EQUAL;
terminal AND, OR;
terminal ASSIGN, ASSIGN_ADDITION, ASSIGN_SUBTRACTION, ASSIGN_MULTIPLICATION, ASSIGN_DIVISION, ASSIGN_MODULO;
terminal INCREMENT, DECREMENT;
terminal SEMICOLON, COMMA, DOT;
terminal PARENTHESES_LEFT, PARENTHESES_RIGHT, BRACKETS_LEFT, BRACKETS_RIGHT, BRACES_LEFT, BRACES_RIGHT;

////////////////////////////////////////////////

// NON TERMINALS
////////////////////////////////////////////////

non terminal Program, ProgramName;
non terminal DeclarationList, Declaration;
non terminal VarDeclarationLine, VarDeclarationList, VarID, VarIDList, VarIDSingle;
non terminal ConstDeclarationLine, ConstIDList, ConstID, Const;
non terminal ClassDeclaration, ClassExtends, ClassName, ClassType;
non terminal MethodDeclarationSection, OptionalMethodDeclarationSection;
non terminal MethodDeclarationList, MethodDeclaration, MethodName;
non terminal FormalParameterSection, FormalParameterList, FormalParameter;
non terminal Type, MethodType, OptionalStatic;
non terminal StatementBlock, StatementList, Statement;
non terminal StatementDesignator, StatementIf, StatementFor, StatementBreak;
non terminal StatementContinue, StatementReturn, StatementRead, StatementPrint;
non terminal DesignatorStatement, OptionalDesignatorStatement;
non terminal Pair<Integer, ValueType> Designator;
non terminal ActualParameterSection, ActualParameterList, ActualParameter;
non terminal MemberList, Member;
non terminal OptionalDimension, Dimension;
non terminal OptionalMethodCall, MethodCall;
non terminal Condition, OptionalCondition, ConditionTermList, ConditionTerm, ConditionFactorList, ConditionFactor;
non terminal OptionalUnaryMinus;
non terminal Pair<Integer, ValueType> Expression;
non terminal Pair<Integer, ValueType> Term, AdditionTermList, Factor, MultiplicationFactorList;
non terminal AdditionOperator, AdditionOperatorLeft, AdditionOperatorRight;
non terminal MultiplicationOperator, MultiplicationOperatorLeft, MultiplicationOperatorRight;
non terminal RelationalOperator, AssignOperator;

precedence left ELSE;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                      PROGRAM
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                                        Program ::= PROGRAM
                                                    {:
                                                        //report_info("Program 1");
                                                    :}
                                                    ProgramName
                                                    {:
                                                        //report_info("Program 2");
                                                    :}
                                                    DeclarationList
                                                    {:
                                                        //report_info("Program 3");
                                                    :}
                                                    MethodDeclarationSection
                                                    {:
                                                        //report_info("Program 4");
                                                        ctx.foundSymbol(SemanticSymbol.PROGRAM_EXIT, null);
                                                    :};

                                    ProgramName ::= ID:progName
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.PROGRAM, progName.toString());
                                                        //report_info("Parsed program name " + progName);
                                                    :};

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                 DECLARATIONS
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                                DeclarationList ::= DeclarationList Declaration
                                                    {:
                                                        report_info("Declaration list Declaration");
                                                    :}
                                                    | /* epsilon */
                                                    {:
                                                        report_info("Declaration list epsilon");
                                                    :};

                                    Declaration ::= VarDeclarationLine
                                                    | ConstDeclarationLine
                                                    | ClassDeclaration;

                                           Type ::= ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.TYPE, id.toString());
                                                        report_info("Matched type " + id);
                                                        RESULT = id;
                                                    :};

                                 OptionalStatic ::= STATIC
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.STATIC, null);
                                                    :}
                                                    | /* epsilon */;

                                     MethodType ::= Type:id
                                                    | VOID
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.TYPE, "void");
                                                    :};
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                        CONSTANTS DECLARATION
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                           ConstDeclarationLine ::= CONST Type ConstIDList SEMICOLON;

                                    ConstIDList ::= ConstIDList COMMA ConstID
                                                    | ConstID;

                                        ConstID ::= ID:constObj
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.CONST, constObj.toString());
                                                        //report_info("ConstDeclaration " + constObj);
                                                    :}
                                                    ASSIGN Const;

                                          Const ::= CONST_NUMBER:val
                                                    {:
                                                        ctx.setConstValue(Integer.parseInt(val.toString()));
                                                    :}
                                                    | CONST_CHAR:val
                                                    {:
                                                        ctx.setConstValue(val.toString().charAt(0));
                                                    :}
                                                    | CONST_BOOL:val
                                                    {:
                                                        ctx.setConstValue(val.toString() == "true");
                                                    :};

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                              VAR DECLARATION
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                             VarDeclarationList ::= VarDeclarationList VarDeclarationLine
                                                    | /* epsilon */;

                             VarDeclarationLine ::= Type VarIDList SEMICOLON;

                                      VarIDList ::= VarIDList COMMA VarIDSingle
                                                    | VarIDSingle;

                                    VarIDSingle ::= VarID:varObj
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.VAR, varObj.toString());
                                                        report_info("VarDeclaration 1 " + varObj);
                                                    :}
                                                    | VarID:varObj
                                                    {:
                                                        report_info("VarDeclaration 3 " + varObj);
                                                    :}
                                                    BRACKETS_LEFT
                                                    {:
                                                        report_info("VarDeclaration 4 " + varObj);
                                                    :}
                                                    BRACKETS_RIGHT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.ARRAY, varObj.toString());
                                                        report_info("VarDeclaration 2 " + varObj);
                                                    :};

                                          VarID ::= ID:id
                                                    {:
                                                        RESULT = id;
                                                    :};

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                            CLASS DECLARATION
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                               ClassDeclaration ::= CLASS ClassName ClassExtends
                                                    BRACES_LEFT VarDeclarationList
                                                    OptionalMethodDeclarationSection BRACES_RIGHT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.CLASS_EXIT, null);
                                                    :};

                                      ClassName ::= ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.CLASS, id.toString());
                                                        report_info("Entered class: " + id);
                                                    :};

                                   ClassExtends ::= EXTENDS ClassType
                                                    {:
                                                        report_info ("Parsed nonempty class extends");
                                                    :}
                                                    | /* epsilon */
                                                    {:
                                                        report_info ("Parsed empty class extends");
                                                    :};

                                      ClassType ::= Type:type
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.TYPE_CLASS, type.toString());
                                                    :};


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                           METHOD DECLARATION
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                       MethodDeclarationSection ::= BRACES_LEFT MethodDeclarationList BRACES_RIGHT;

               OptionalMethodDeclarationSection ::= MethodDeclarationSection
                                                    | /* epsilon */;

                          MethodDeclarationList ::= MethodDeclarationList MethodDeclaration
                                                    | /* epsilon */;

                              MethodDeclaration ::= OptionalStatic MethodType MethodName
                                                    PARENTHESES_LEFT FormalParameterSection PARENTHESES_RIGHT
                                                    VarDeclarationList
                                                    BRACES_LEFT StatementList BRACES_RIGHT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.METHOD_EXIT, null);
                                                    :};

                                    MethodName ::= ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.METHOD, id.toString());
                                                        report_info("Entered method: " + id);
                                                    :};

                        FormalParameterSection ::= FormalParameterList
                                                    | /* epsilon */;

                           FormalParameterList ::= FormalParameterList COMMA FormalParameter
                                                    | FormalParameter;

                                FormalParameter ::= Type ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.FORMAL_PARAMETER, id.toString());
                                                    :}
                                                    | Type ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.FORMAL_PARAMETER, id.toString());
                                                    :}
                                                    BRACKETS_LEFT BRACKETS_RIGHT;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                   STATEMENTS
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                                 StatementBlock ::= BRACES_LEFT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.STATEMENT_BLOCK, null);
                                                    :}
                                                    StatementList BRACES_RIGHT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.STATEMENT_BLOCK_EXIT, null);
                                                    :};

                                  StatementList ::= StatementList Statement
                                                    | /* epsilon */;

                                      Statement ::= StatementDesignator
                                                    | StatementIf
                                                    | StatementFor
                                                    | StatementBreak
                                                    | StatementContinue
                                                    | StatementReturn
                                                    | StatementRead
                                                    | StatementPrint
                                                    | StatementBlock;

                            StatementDesignator ::= DesignatorStatement SEMICOLON;

                                    StatementIf ::= IF PARENTHESES_LEFT Condition PARENTHESES_RIGHT Statement
                                                    | IF PARENTHESES_LEFT Condition PARENTHESES_RIGHT Statement
                                                    ELSE Statement;

                                   StatementFor ::= FOR PARENTHESES_LEFT
                                                    OptionalDesignatorStatement SEMICOLON
                                                    OptionalCondition SEMICOLON
                                                    OptionalDesignatorStatement SEMICOLON
                                                    PARENTHESES_RIGHT Statement;

                                 StatementBreak ::= BREAK SEMICOLON;

                              StatementContinue ::= CONTINUE SEMICOLON;

                                StatementReturn ::= RETURN SEMICOLON
                                                    | RETURN Expression SEMICOLON;

                                  StatementRead ::= READ PARENTHESES_LEFT Designator PARENTHESES_RIGHT SEMICOLON;

                                 StatementPrint ::= PRINT PARENTHESES_LEFT Expression PARENTHESES_RIGHT;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                         DESIGNATOR STATEMENT
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                    OptionalDesignatorStatement ::= DesignatorStatement
                                                    | /* epsilon */;

                            DesignatorStatement ::= Designator AssignOperator Expression
                                                    | Designator MethodCall
                                                    | Designator INCREMENT
                                                    | Designator DECREMENT;

                         ActualParameterSection ::= ActualParameterList
                                                    | /* epsilon */;

                            ActualParameterList ::= ActualParameterList COMMA ActualParameter
                                                    | ActualParameter;

                                ActualParameter ::= Expression;

                                     Designator ::= ID:id
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.DESIGNATOR, id.toString());
                                                        Obj found = Tab.find(id.toString());
                                                        if (found == Tab.noObj) {
                                                            RESULT = EMPTY_VALUE;
                                                        } else {
                                                            RESULT = new Pair<Integer, ValueType>(found.getType().getKind(), (found.getType().getKind() == Obj.Var)?ValueType.LVALUE:ValueType.RVALUE);
                                                        }
                                                    :}
                                                    MemberList;

                                     MemberList ::= MemberList Member
                                                    | /* epsilon */;

                                         Member ::= DOT ID
                                                    | Dimension;

                              OptionalDimension ::= Dimension
                                                    | /* epsilon */;

                             OptionalMethodCall ::= MethodCall
                                                    | /* epsilon */;

                                     MethodCall ::= PARENTHESES_LEFT ActualParameterSection PARENTHESES_RIGHT
                                                    {:
                                                        ctx.foundSymbol(SemanticSymbol.METHOD_CALL, null);
                                                    :};

                                      Dimension ::= BRACKETS_LEFT Expression BRACKETS_RIGHT;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                    CONDITION
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                              OptionalCondition ::= Condition
                                                    | /* epsilon */;

                                      Condition ::= ConditionTermList ;

                              ConditionTermList ::= ConditionTermList OR ConditionTerm
                                                    | ConditionTerm;

                                  ConditionTerm ::= ConditionFactorList;

                            ConditionFactorList ::= ConditionFactorList AND ConditionFactor
                                                    | ConditionFactor;

                                ConditionFactor ::= Expression
                                                    | Expression RelationalOperator Expression;

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                   Expression
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                                     Expression ::= AdditionTermList:pair
                                                    {:
                                                        RESULT = pair;
                                                    :}
                                                    | SUBTRACTION AdditionTermList:pair
                                                    {:
                                                        if (pair.getFirst() != Struct.Int) {
                                                            //error!
                                                        }
                                                        RESULT = pair;
                                                    :};


                               AdditionTermList ::= AdditionTermList:pair1 AdditionOperator Term:pair2
                                                    {:
                                                        if (pair1.getFirst() != pair2.getFirst()) {
                                                            report_error("not compatible types: " + pair1.getFirst() + " " + pair2.getFirst());
                                                            RESULT = EMPTY_VALUE;
                                                        } else if (pair1.getFirst() != Struct.Int) {
                                                            report_error("types same, but not integer: " + pair1.getFirst());
                                                            RESULT = EMPTY_VALUE;
                                                        } else {
                                                            RESULT = new Pair(pair1.getFirst(), ValueType.RVALUE);
                                                        }

                                                    :}
                                                    | Term:pair
                                                    {:
                                                        RESULT = pair;
                                                    :};

                                           Term ::= MultiplicationFactorList:pair
                                                    {:
                                                        RESULT = pair;
                                                    :};

                       MultiplicationFactorList ::= MultiplicationFactorList:pair1 MultiplicationOperator Factor:pair2
                                                    {:
                                                        if (pair1.getFirst() != pair2.getFirst()) {
                                                            report_error("not compatible types " + pair1.getFirst() + " " + pair2.getFirst());
                                                            RESULT = EMPTY_VALUE;
                                                        } else if (pair1.getFirst() != Struct.Int) {
                                                            report_error("types same, but not integer: " + pair1.getFirst());
                                                            RESULT = EMPTY_VALUE;
                                                        } else {
                                                            RESULT = new Pair(pair1.getFirst(), ValueType.RVALUE);
                                                        }

                                                    :}
                                                    | Factor:pair
                                                    {:
                                                        RESULT = pair;
                                                    :};

                                         Factor ::= Designator:pair
                                                    {:
                                                        RESULT = pair;
                                                    :}
                                                    | Designator:pair MethodCall
                                                    {:
                                                        RESULT = pair;
                                                    :}
                                                    | CONST_NUMBER
                                                    {:
                                                        RESULT = new Pair<Integer, ValueType>(Struct.Int, ValueType.RVALUE);
                                                    :}
                                                    | CONST_CHAR
                                                    {:
                                                        RESULT = new Pair<Integer, ValueType>(Struct.Char, ValueType.RVALUE);
                                                    :}
                                                    | CONST_BOOL
                                                    {:
                                                        RESULT = new Pair<Integer, ValueType>(Struct.Bool, ValueType.RVALUE);
                                                    :}
                                                    | NEW Type OptionalDimension
                                                    {:
                                                        RESULT = new Pair<Integer, ValueType>(Struct.Int, ValueType.RVALUE);
                                                    :}
                                                    | PARENTHESES_LEFT Expression:pair PARENTHESES_RIGHT
                                                    {:
                                                        RESULT = pair;
                                                    :};


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//                                    OPERATORS
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

                               AdditionOperator ::= AdditionOperatorLeft
                                                    | AdditionOperatorRight;

                           AdditionOperatorLeft ::= ADDITION
                                                    | SUBTRACTION;

                          AdditionOperatorRight ::= ASSIGN_ADDITION
                                                    | ASSIGN_SUBTRACTION;

                         MultiplicationOperator ::= MultiplicationOperatorLeft
                                                    | MultiplicationOperatorRight;

                     MultiplicationOperatorLeft ::= MULTIPLICATION
                                                    | DIVISION
                                                    | MODULO;

                    MultiplicationOperatorRight ::= ASSIGN_MULTIPLICATION
                                                    | ASSIGN_DIVISION
                                                    | ASSIGN_MODULO;

                             RelationalOperator ::= EQUAL
                                                    | NOT_EQUAL
                                                    | GREATER
                                                    | GREATER_EQUAL
                                                    | LESS
                                                    | LESS_EQUAL;

                                 AssignOperator ::= ASSIGN
                                                    | AdditionOperatorRight
                                                    | MultiplicationOperatorRight;



