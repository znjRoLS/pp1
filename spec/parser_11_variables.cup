 VarDeclarationList ::= VarDeclarationList VarDeclarationLine
                        | /* epsilon */;

 VarDeclarationLine ::= Type VarIDList SEMICOLON
                        | Type error SEMICOLON:obj
                        {:
                            ctx.foundSymbol(
                                SemanticSymbol.ERROR_RECOVERED,
                                objleft,
                                new SemanticParameters()
                                    .setValue(ErrorType.GLOBAL_VAR)
                            );
                        :};

          VarIDList ::= VarIDList COMMA VarIDSingle
                        | VarIDSingle
                        | error COMMA:obj VarIDSingle
                        {:
                            ctx.foundSymbol(
                                SemanticSymbol.ERROR_RECOVERED,
                                objleft,
                                new SemanticParameters()
                                    .setValue(ErrorType.GLOBAL_VAR)
                            );
                        :};

        VarIDSingle ::= VarID:varName
                        {:
                            // String varName;
                            ctx.foundSymbol(
                                SemanticSymbol.VAR,
                                varNameleft,
                                new SemanticParameters()
                                    .setName(varName)
                            );

                        :}
                        | VarID:varName BRACKETS_LEFT BRACKETS_RIGHT
                        {:
                            // String varName;
                            ctx.foundSymbol(
                                SemanticSymbol.ARRAY,
                                varNameleft,
                                new SemanticParameters()
                                    .setName(varName)
                            );
                        :};

              VarID ::= ID:id
                        {:
                            // String id;
                            RESULT = id;
                        :};