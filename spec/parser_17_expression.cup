                 Expression ::= AdditionTermList:expression
                                {:
                                    // ExpressionToken expression;

                                    RESULT = expression;
                                :}
                                | SUBTRACTION AdditionTermList:expression
                                {:
                                    // ExpressionToken expression;

                                    ctx.foundSymbol(SemanticSymbol.SINGLE_EXPRESSION,
                                        new SemanticParameters()
                                        .setExpression(expression)
                                        .setType("int"));

                                    RESULT = expression;
                                :};


           AdditionTermList ::= AdditionTermList:expression1 AdditionOperator:operator Term:expression2
                                {:
                                    // ExpressionToken expression1;
                                    // ExpressionToken expression2;
                                    // Integer operator;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.EXPRESSION,
                                        new SemanticParameters()
                                        .setExpression(expression1)
                                        .setExpression2(expression2)
                                        .setType("int")
                                        .setValue(operator));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | Term:expression
                                {:
                                    // ExpressionToken expression;

                                    RESULT = expression;
                                :};

                       Term ::= MultiplicationFactorList:expression
                                {:
                                    // ExpressionToken expression;

                                    RESULT = expression;
                                :};

   MultiplicationFactorList ::= MultiplicationFactorList:expression1 MultiplicationOperator:operator Factor:expression2
                                {:
                                    // ExpressionToken expression1;
                                    // ExpressionToken expression2;
                                    // Integer operator;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.EXPRESSION,
                                        new SemanticParameters()
                                        .setExpression(expression1)
                                        .setExpression2(expression2)
                                        .setType("int")
                                        .setValue(operator));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | Factor:expression
                                {:
                                    // ExpressionToken expression;

                                    RESULT = expression;
                                :};

                     Factor ::= Designator:varName
                                {:
                                    // String varName;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.DESIGNATOR_FACTOR,
                                        new SemanticParameters()
                                        .setName(varName));

                                    report_debug("test " + objectType.getKind());
                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | Designator:function MethodCall
                                {:
                                    // String function;
                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.METHOD_CALL_FACTOR,
                                        new SemanticParameters()
                                        .setName(function));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | CONST_NUMBER:val
                                {:
                                    // Integer val;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.CONST_FACTOR,
                                        new SemanticParameters()
                                        .setValue(val)
                                        .setType("int"));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | CONST_CHAR:val
                                {:
                                    // char val;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.CONST_FACTOR,
                                        new SemanticParameters()
                                        .setValue((int)val)
                                        .setType("char"));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | CONST_BOOL:val
                                {:
                                    // boolean val;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.CONST_FACTOR,
                                        new SemanticParameters()
                                        .setValue(val?1:0)
                                        .setType("bool"));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | NEW Type:type OptionalDimension
                                {:
                                    // String type;

                                    Struct objectType = ctx.foundSymbol(SemanticSymbol.NEW,
                                        new SemanticParameters()
                                        .setName(type));

                                    RESULT = new ExpressionToken(objectType, false);
                                :}
                                | PARENTHESES_LEFT Expression:expr PARENTHESES_RIGHT
                                {:
                                    // ExpressionToken expr;

                                    RESULT = expr;
                                :};